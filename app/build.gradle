apply plugin: 'com.android.application'
apply plugin: 'dev.rikka.tools.refine'

android {
    compileSdk rootProject.ext.compileSdk
    ndkVersion rootProject.ext.ndkVersion
    namespace "top.canyie.dreamland"

    defaultConfig {
        applicationId "top.canyie.dreamland"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled false
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DRIRU_MODULE_VERSION_NAME=$rootProject.ext.versionName",
                        "-DRIRU_NEW_MODULE_API_VERSION=$rootProject.ext.riruNewApiVersion",
                        "-DDREAMLAND_VERSION_CODE=$rootProject.ext.versionCode",
                        '-DANDROID_STL=none' // built-in libcxx introduced exception, so use our own instead
            }
        }
        ndk {
            moduleName "riru_dreamland"
            abiFilters  "arm64-v8a","armeabi-v7a"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    buildFeatures {
        prefab true
        aidl true
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    compileOnly 'androidx.annotation:annotation:1.6.0'
    implementation 'top.canyie.pine:core:0.2.8'
    implementation 'top.canyie.pine:xposed:0.1.0'
    implementation 'top.canyie.pine:enhances:0.0.4'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'dev.rikka.tools.refine:runtime:4.3.0'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'
    implementation "io.github.vvb2060.ndk:dobby:1.2"
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
    compileOnly project(':hiddenapi-stubs')
    testImplementation 'junit:junit:4.12'
    //androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

def moduleProp = [
        id: 'riru_dreamland',
        name: 'Dreamland',
        author: 'canyie',
        version: rootProject.ext.versionName + "_" + rootProject.ext.versionCode,
        versionCode: rootProject.ext.versionCode,
        description: 'A third-party Xposed framework implementation, which supports Android 5.0 ~ 14. Requires module \'Riru\' is installed or Zygisk is enabled. Telegram group: @DreamlandFramework, QQ group: 949888394.',
        api: rootProject.ext.riruApiVersion
]

def adb = android.adbExecutable
def jarName = 'dreamland.jar'
def nativeAbiToSystemDirMap = ['armeabi-v7a': 'lib', 'arm64-v8a': 'lib64']
def nativeLibName = 'libriru_dreamland.so'
def releasePath = "$buildDir/outputs/magisk"

afterEvaluate {
    android.applicationVariants.forEach { variant ->
        def variantCapped = variant.name.capitalize()
        def variantLowered = variant.name.toLowerCase()
        def tempPath = "$buildDir/intermediates/magisk/${variantLowered}"
        def tempTemplatePath = "$tempPath/template"
        def jarOutputDir = "$tempTemplatePath/"
        def jarOutputPath = "$jarOutputDir/$jarName"
        def nativeDir = "$tempTemplatePath/riru/"
        def basename = "dreamland-${moduleProp['versionCode']}-${variantLowered}"
        def outZip = "$releasePath/${basename}.zip"

        def templateTask = tasks.register("copyMagiskTemplate${variantCapped}", Copy) {
            dependsOn("assemble$variantCapped")
            from file("$rootDir/template")
            into file(tempTemplatePath)

            inputs.properties moduleProp
            doLast {
                file("$tempTemplatePath/module.prop").withWriter('utf-8') { writer ->
                    moduleProp.each { name, value -> writer.write("$name=$value\n") }
                }

                copy {
                    from file("$rootDir/README.md")
                    into file(tempTemplatePath)
                }
            }
        }

        def nativeTask = tasks.register("copyNativeLibs${variantCapped}") {
            dependsOn("copyMagiskTemplate$variantCapped")
            def stripTask = tasks.findByName("strip${variantCapped}DebugSymbols")
            doLast {
                stripTask.outputs.files.each { baseInputDir ->
                    nativeAbiToSystemDirMap.each { origin, to ->
                        copy {
                            from file("$baseInputDir/lib/$origin/$nativeLibName")
                            into file("$nativeDir/$to")
                        }
                    }
                }
            }

            outputs.upToDateWhen { stripTask.state.upToDate && file(nativeDir).exists() }
        }

        def jarTask = tasks.register("jarClassesDex${variantCapped}") {
            dependsOn("copyNativeLibs$variantCapped")
            def mergeDexTask = variantLowered == 'release'
                    ? tasks.findByName("minifyReleaseWithR8")
                    : tasks.findByName('mergeDexDebug')
            doLast {
                def dex = variantLowered == 'release'
                        ? mergeDexTask.outputs.files.filter { it.path.contains('minifyReleaseWithR8') }
                        : mergeDexTask.outputs.files
                copy {
                    from dex
                    into file(jarOutputDir)
                    rename 'classes.dex', 'dreamland.jar'
                }
            }

            outputs.upToDateWhen { mergeDexTask.state.upToDate && file(jarOutputPath).exists() }
        }

        def zipTask = tasks.register("zipMagiskFiles${variantCapped}", Zip) {
            dependsOn("jarClassesDex$variantCapped")
            from file(tempTemplatePath)
            destinationDirectory = layout.buildDirectory.dir(releasePath)
            archiveBaseName = basename
            outputs.upToDateWhen {
                templateTask.get().state.upToDate
                        && nativeTask.get().state.upToDate
                        && jarTask.get().state.upToDate
            }
        }

        tasks.register("assembleMagisk${variantCapped}") {
            dependsOn("zipMagiskFiles${variantCapped}")
            doLast {
                println "Artifacts are saved to $outZip"
            }
            outputs.upToDateWhen { zipTask.get().state.upToDate }
        }

        tasks.register("assembleAndFlashMagisk${variantCapped}") {
            dependsOn "assembleMagisk${variantCapped}"
            doLast {
                exec { execSpec ->
                    executable adb
                    args "shell", "rm", "-f", "/data/local/tmp/${basename}.zip"
                }

                exec { execSpec ->
                    executable adb
                    args "push", "$outZip", "/data/local/tmp/${basename}.zip"
                }

                exec { execSpec ->
                    executable adb
                    args "shell", "su", "-M", "-c", "magisk --install-module /data/local/tmp/${basename}.zip"
                }
            }
            outputs.upToDateWhen { false }
        }

        tasks.register("assembleAndFlashMagisk${variantCapped}AndReboot", Exec) {
            dependsOn "assembleAndFlashMagisk${variantCapped}"
            doFirst {
                commandLine adb, "shell", "svc power reboot || reboot"
            }
        }
    }
}

tasks.register('rebootSafemode', Exec) {
    doFirst {
        exec { execSpec ->
            executable adb
            args "shell", "su", "-c", "setprop", "persist.sys.safemode", "1"
        }
        commandLine adb, "reboot"
    }
}

tasks.register('invaildateReboot', Exec) {
    doFirst {
        exec { execSpec ->
            executable adb
            args "shell", "su", "-c", "touch", "/data/adb/modules/riru_dreamland/disable"
        }
        commandLine adb, "reboot"
    }
}
