apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    // ndk version is required on my computer but not required on CI
    ndkVersion rootProject.ext.ndkVersion

    defaultConfig {
        applicationId "top.canyie.dreamland"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled false
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                def flags = "-ffixed-x18" +
                        " -fvisibility=hidden" +
                        " -fvisibility-inlines-hidden" +
                        " -fno-rtti" + " -fno-exceptions" +
                        " -Wno-builtin-macro-redefined" +
                        " -D__FILE__=__FILE_NAME__"
                cFlags "-Wall -Wextra $flags"
                cppFlags "-std=c++17 $flags"
                arguments "-DRIRU_MODULE_VERSION_NAME=$rootProject.ext.versionName",
                        "-DRIRU_NEW_MODULE_API_VERSION=$rootProject.ext.riruNewApiVersion",
                        "-DDREAMLAND_VERSION_CODE=$rootProject.ext.versionCode"
            }
        }
        ndk {
            moduleName "riru_dreamland"
            abiFilters  "arm64-v8a","armeabi-v7a"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false // TODO: Enable minify
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.6.0" // Available on Azure Pipelines CI
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.annotation:annotation:1.1.0'
    // The latest pine not published to online repo yet, just direct use aar
    //implementation 'top.canyie.pine:core:0.2.5'
    //implementation 'top.canyie.pine:xposed:0.0.8'
    //implementation 'top.canyie.pine:enhances:0.0.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    compileOnly files('lib/hiddenapis-stub.jar')
    testImplementation 'junit:junit:4.12'
    //androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

def hiddenApiStubs = "$projectDir/lib/hiddenapis-stub.jar"

preBuild.doLast {
    if (!file(hiddenApiStubs).exists())
        throw new IllegalStateException("Hidden api stubs not found. " +
                "Please execute gradle task :hiddenapi-stubs:jarStubApis to generate it.")

    def imlFile = file(project.name + ".iml")
    try {
        def parsedXml = new groovy.util.XmlParser().parse(imlFile)
        def jdkNode = parsedXml.component[1].orderEntry.find { it.'@type' == 'jdk' }
        parsedXml.component[1].remove(jdkNode)
        def sdkString = "Android API $rootProject.ext.compileSdkVersion Platform"
        new groovy.util.Node(parsedXml.component[1], 'orderEntry', ['type': 'jdk', 'jdkName': sdkString, 'jdkType': 'Android SDK'])
        groovy.xml.XmlUtil.serialize(parsedXml, new FileOutputStream(imlFile))
    } catch (FileNotFoundException ignored) {
        // nop, iml not found
    }
}

afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs.add("-Xbootclasspath/p:$hiddenApiStubs")
    }
}

def moduleProp = [
        id: 'riru_dreamland',
        name: 'Riru-Dreamland',
        author: 'canyie',
        version: rootProject.ext.versionName + "_" + rootProject.ext.versionCode,
        versionCode: rootProject.ext.versionCode,
        description: 'A third-party Xposed framework implementation, supports Android 7.0 ~ 11. Requires module \'Riru\' is installed. Telegram group: @DreamlandFramework, QQ group: 949888394.',
        minMagisk: rootProject.ext.minMagiskVersion,
        api: rootProject.ext.riruApiVersion
]

def tempPath = "$buildDir/intermediates/magisk"
def tempTemplatePath = "$tempPath/template"
def localDir = "$tempPath/local"
def jarName = 'dreamland.jar'
def jarOutputDir = "$tempTemplatePath/system/framework"
def jarOutputPath = "$jarOutputDir/$jarName"
def nativeDir = "$tempTemplatePath/riru/"
def nativeAbiToSystemDirMap = ['armeabi-v7a': 'lib', 'arm64-v8a': 'lib64']
def nativeLibName = 'libriru_dreamland.so'
def releasePath = "$buildDir/outputs/magisk"

task copyMagiskTemplate(type: Copy) {
    from file("$rootDir/template")
    into file(tempTemplatePath)

    inputs.properties moduleProp
    doLast {
        file("$tempTemplatePath/module.prop").withWriter('utf-8') { writer ->
            moduleProp.each { name, value -> writer.write("$name=$value\n") }
        }

        copy {
            from file("$rootDir/README.md")
            into file(tempTemplatePath)
        }
    }
}

task copyNativeLibs() {
    doLast {
        stripReleaseDebugSymbols.outputs.files.each { baseInputDir ->
            nativeAbiToSystemDirMap.each { origin, to ->
                copy {
                    from file("$baseInputDir/lib/$origin/$nativeLibName")
                    into file("$nativeDir/$to")
                }
            }
        }
    }

    // FIXME: stripReleaseDebugSymbols.state.upToDate is always false
    onlyIf { !stripReleaseDebugSymbols.state.upToDate || !file(nativeDir).exists() }
}

task jarClassesDex() {
    doLast {
        copy {
            from mergeDexRelease.outputs.files
            into file(jarOutputDir)
            rename 'classes.dex', 'dreamland.jar'
        }
    }

    // FIXME: the task always up-to-date, disable up-to-date now
    // onlyIf { !mergeDexRelease.state.upToDate || !file(jarOutputPath).exists() }
    outputs.upToDateWhen { false }
}

/*task jarClassesDex(type: Jar) {
    def dexDir = file("$localDir/dex")

    doFirst {
        copy {
            from mergeDexRelease.outputs.files
            into dexDir
        }
    }

    from dexDir
    destinationDir file(jarOutputDir)
    archiveName jarName

    // FIXME: the task always up-to-date, disable up-to-date now
    // onlyIf { !mergeDexRelease.state.upToDate || !file(jarOutputPath).exists() }
    outputs.upToDateWhen { false }
}*/

task zipMagiskFiles(type: Zip) {
    from file(tempTemplatePath)
    destinationDir file(releasePath)
    baseName "magisk-dreamland-${moduleProp['versionCode']}"
}

task assembleMagiskRelease(type: GradleBuild, dependsOn: build) {
    tasks = [
            'copyMagiskTemplate',
            'copyNativeLibs',
            'jarClassesDex',
            'zipMagiskFiles'
    ]
}
