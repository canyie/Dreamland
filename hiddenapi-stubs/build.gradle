apply plugin: 'com.android.library'

android {
    compileSdk rootProject.ext.compileSdk
    namespace 'top.canyie.dreamland.hiddenapi'
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def refinePluginVersion = '4.3.0'
    annotationProcessor "dev.rikka.tools.refine:annotation-processor:$refinePluginVersion"
    compileOnly "dev.rikka.tools.refine:annotation:$refinePluginVersion"
}

task jarStubApis(type: Jar, dependsOn: build) {
    archiveBaseName = 'hiddenapis-stub'
    from("$projectDir/build/intermediates/javac/release/classes/")
    destinationDirectory = layout.buildDirectory.dir("$rootDir/app/lib/")
    exclude 'BuildConfig.class', 'R.class'
    exclude { it.name.startsWith('R$') }
}
